!:
::  eyre (4e), http server
::
  |=  pit=vase
  ^-  vane                                              ::  kernel instrument
  =|  $:  seh=(list ,[p=host q=seat])                   ::  host permissions
      ==                                                ::
  |=  [now=@da eny=@ sky=||(* (unit))]                  ::  activate
  ^?                                                    ::  opaque core
  |%                                                    ::
  ++  beat                                              ::  process move
    |=  [whu=(unit seat) pax=tire hen=hose fav=card]
    ^-  [p=(list move) q=vane]
    ?+    -.fav
      [[[whu hen fav] ~] ..^$]
    ::
        %bind                                           ::  register server
      ?>  =(%gold (adit hen))
      =+  ^=  nom  ^-  tape
          ?-  -.q.fav
            &  ~(ram re (dish:ut [~ %path] p.q.fav))
            |  ~(rend co ~ %if p.q.fav)
          ==
      =+  giy=~(rend co ~ %p p.fav)
      :-  [[~ [/d/ hen] [%flog %warn "http: serving {nom} as {giy}"]] ~]
      ^|
      %=    ..^$
          seh
        :-  [q.fav p.fav]
        (skip seh |=([a=host b=seat] (hone q.fav a)))
      ==
    ::
        ?(%thin %this)                                  ::  http request
      :_  ..^$
      :_  ~
      =+  heq=(thin =(%this -.fav) p.fav)
      =+  ^=  whu  |-  ^-  (unit ,@p)
          ?~  seh  ~
          ?:  (hone r.p.p.heq p.i.seh) 
            [~ q.i.seh] 
          $(seh t.seh)
      ?~  whu
        [~ hen [%thou 404 ~ ~]]
      [whu [/b/ /e/ hen] [%thee heq]]
    ::
        %that                                           ::  http response
      :_  ..^$
      :_  ~
      :+  ~  hen
      :-  %thou
      ^-  httr
      ?-  -.p.fav
        %mid  [200 ~[content-type/(moon p.p.fav)] [~ q.p.fav]]
        %ham  [200 ~[content-type/'text/html'] [~ (tact (xmlt p.p.fav ~))]]
        %raw  p.p.fav
      ==
    ==
  ::
  ++  come  |=(old=vase (load old))
  ::
  ++  doze
    |=  [now=@da hen=hose]
    ^-  (unit ,@da)
    ~
  ::
  ++  flee  stay
  ++  load
    |=  new=vase
    ^-  vane
    ?.  (~(nest ut -:!>(seh)) & p.new)
      ~|(%load-nest-dill !!)
    ..^$(seh ((list ,[p=host q=seat]) q.new))
  ::
  ++  raze  
    ^-  vane
    ..$(seh ~)
  ::
  ++  scry
    |=  [our=seat ren=@tas who=seat lot=coin tyl=path]
    ^-  (unit)
    ~
  ::
  ++  stay  `vase`!>(seh)
  --

