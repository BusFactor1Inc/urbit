:: some forms of mill
::

|:  z = *
==
  |*  (open (gene z))
  ::
    - open
      ||  gene
        gen = gene
      ==
        |*
          ?-  gen
            -   [%bink mef=form]
              (form-gate mef.gen)

            -   [%blem gix=gene]
              [%quiz gix.gen [%rock 1] [%rock 0]]

            -   [%blin bov=gene vab=grid]
              +:  %link
                [%malk vab.gen]
                bov.gen

            -   [%boce das=(list gene)]
              |-
                ?-  das.gen
                  -   ~   [%rock 0]
                  -   ^   [%quiz i.das.gen $(das.gen t.das.gen) [%rock 1]]
                ==

            -   [%cage fut=(list gene)]
              gen

            -   [%cell yeg=gene nal=gene]
              [%cage yeg.gen nal.gen ~]

            -   [%cast fes=gene rum=gene]
              gen

            -   [%colb del=gene zim=gene]
              [%link zim.gen del.gen]

            -   [%drol nix=form tep=form von=gene]
              +:  %trop
                (form-lump tep.gen) 
                [%lome [%cast (form-lump nix.gen) von.gen]]

            -   [%feng tes=gene bif=gene]
              [%quiz tes.gen bif.gen [%bail ~]]

            -   [%flic tep=form von=gene]
              [%trop (form-lump tep.gen) [%lift %% von.gen]]

            -   [%dant das=(list gene)]
              |-
                ?-  das.gen
                  -   ~   [%rock 1]
                  -   ^   [%quiz i.das.gen [%rock 0] $(das.gen t.das.gen)]
                ==

            -   [%flot cug=gene]
              +:  %link
                [%load %% cug.gen]
                [%port 0 %%]

            -   [%frag haz=axis]
              [%kick [gen ~] ~]

            -   [%garc buz=gene pum=(list gene)]
              ?-  pum.gen
                -   ~   [%flec buz.gen]
                -   ^   [%mang buz.gen [%cage pum.gen]]
              ==

            -   [%gril mox=wire bem=(list pike)]
              |-
                ?-  bem.gen
                  -   ~
                    [%bail ~]
                  -   ^
                    =>  a   
                      (open-pike mox.gen i.bem.gen)
                    [%quiz tes.a bif.a $(bem.gen t.bem.gen)]
                ==

            -   [%grun vec=bolt sud=gene]
              +:  %link
                [%kick [[%frag 1] ~] vec.gen]
                sud.gen

            -   [%kick ved=wire suc=bolt]
              gen

            -   [%lift pir=book]
              gen

            -   [%link del=gene zim=gene]
              gen

            -   [%load pir=book]
              gen

            -   [%mang buz=gene lep=gene]
              +:  %trop
                buz.gen
                +:  %kick
                  [[%frag 2] %% ~]
                  +~
                    +-  [[%frag 4] ~]
                      [%link [%frag 3] lep.gen]
                  ==

            -   [%lome cug=gene]
              [%load %% cug.gen]

            -   [%lorb tul=form rum=gene] 
              [%cast (form-lump tul.gen) rum.gen]

            -   [%malk vab=grid]
              +-  %load
                =>  pir   ~book.~
                |-
                  ?-  vab.gen
                    -   ~   pir
                    -   ^   
                      $(vab.gen t.vab.gen, 
                        pir (bush-add pir p.i.vab.gen q.i.vab.gen))
                  ==
                  
            -   [%pont han=mark dur=gene leb=gene]
              +:  %link
                [%cage [%name han.gen dur.gen] [%frag 1] ~]
                leb.gen

            -   [%port tic=@ vix=mark]
              [%kick [gen ~] ~]

            -   [%prec hig=gene muc=gene]
              +:   %link
                [%cage muc.gen [%frag 1] ~]
                hig.gen

            -   [%prox tes=gene hob=gene]
              [%quiz tes.gen [%bail ~] hob.gen]

            -   [%quiz tes=gene bif=gene hob=gene]
              gen

            -   [%qual dor=gene vit=gene lum=gene sag=gene]
              [%cage dor.gen vit.gen lum.gen sag.gen ~]

            -   [%rack der=(list gene)]
              +-  %cage
                |-
                  ?-  der
                    -   ~   [[%rock 0] ~]
                    -   ^   [i.der $(der t.der)]
                  ==

            -   [%rald rum=gene]
              [%cast [%nock %3 [%rock 0]] rum.gen]

            -   [%rulf han=mark dur=gene leb=gene]
              +:  %link
                [%cage [%name han.gen leb.gen] [%frag 1] ~]
                dur.gen

            -   [%stam feg=gene bem=(list pike)]
              [%trop feg.gen [%gril [[%frag 2] ~] bem.gen]]

            -   [%trel ruv=gene sel=gene gar=gene]
              [%cage ruv.gen sel.gen gar.gen ~]

            -   [%sure fes=gene gav=gene vep=gene]
              gen

            -   [%trop hig=gene muc=gene] 
              +:  %link
                [%cage hig.gen [%frag 1] ~]
                muc.gen
          ==

          - open-pike
            ||  [tes=gene bif=gene]
              mox = wire
              pik = pike
            ==
              ?-  pik
                -   [%lask kel=form luf=gene]           ::  -
                  +-
                    [%like mox (form-grip kel.pik)]
                    [%sure (form-lump kel.pik) [%kick mox ~] luf.pik]

                -   [%plic kel=form]                    ::  +
                  $(pik [%lask kel.pik [%kick mox ~]])

                -   [%semp kel=form luf=gene]           ::  :
                  +-
                    [%like mox (form-grip kel.pik)]
                    +:  %grun
                      +~
                        +-  mox
                          +:  %mang
                            (form-gate kel.pik)
                            [%cast [%crad 0] [%kick mox ~]]
                      ==
                      luf.pik

                -   [%fing kel=form]                    ::  =
                  +-
                    [%like mox (form-grip kel.pik)]
                    +:  %mang
                      (form-gate kel.pik)
                      [%cast [%crad 0] [%kick mox ~]]

                -   [%homp luf=gene]                    ::  *
                  +-
                    [%rock 0]
                    luf.pik
              ==

          - form-grip
            ||  gene
              kel = form
            ==
              ?-  kel
                -   %blur   [%crad 0]
                -   %atom   [%palt 0]
                -   %flag   [%nock %6 [%cage [%rock 0] [%rock 0] ~]]
                -   %twin   $(kel [%crib [%% %blur] [%% %blur] ~])

                -   [%crib wix=(list :[p=mark q=form])]
                  +-  %cage
                    (turn wix.kel |:{[p=mark q=form] [%name p (form-grip q)]})

                -   [%pick bec=(list form)]
                  +:  %trop
                    [%cast [%crad 0] $(kel ?-{bec.kel: - ^ i.bec.kel})]
                    +:  %gril
                      [[%frag 2] ~]
                      |-
                        ?-  bec.kel
                          +   ~ 
                          -   ^   [[%fing i.bec.kel] $(bec.kel t.bec.kel)]
                        ==
       
                -   [%rock hin=*]               [%rock hin.kel]
                -   [%gate puz=gene]            [%crad 0]
                -   [%grip ved=form piv=form]   $(kel ved.kel)
                -   [%dish heb=gene]            heb.kel 
              ==

          - form-lump
            ||  gene
              kel = form
            ==
              ?-  kel
                -   %blur   [%crad 0]
                -   %atom   [%palt 0]
                -   %flag   [%nock %6 [%cage [%rock 0] [%rock 0] ~]]
                -   %twin   $(kel [%crib [%% %blur] [%% %blur] ~])

                -   [%crib wix=(list :[p=mark q=form])]
                  +-  %cage
                    (turn wix.kel |:{[p=mark q=form] [%name p (form-lump q)]})

                -   [%pick bec=(list form)]
                  +:  %trop
                    [%cast [%crad 0] $(kel ?-{bec.kel: - ^ i.bec.kel})]
                    +:  %gril
                      [[%frag 2] ~]
                      |-
                        ?-  bec.kel
                          +   ~ 
                          -   ^   [[%fing i.bec.kel] $(bec.kel t.bec.kel)]
                        ==
     
                -   [%rock hin=*]               [%rock hin.kel]
                -   [%gate puz=gene]            [%link puz.kel [%port 0 %%]]
                -   [%grip ved=form piv=form]   $(kel piv.kel)
                -   [%dish heb=gene]            heb.kel
              ==


          - form-gate
            ||  gene
              kel = form
            ==
              ?-  kel
                -   %blur   
                  [%trop [%crad 0] %lome [%frag 4]]

                -   %atom 
                  +:  %trop
                    [%crad 0]
                    +-  %lome
                      [%feng [%like [[%frag 4] ~] [%palt 0]] [%frag 4]]

                -   %flag
                  +:  %trop
                    [%crad 0]
                    +-  %lome
                      [%nock %6 [%cage [%rock 0] [%frag 4] ~]]

                -   %twin
                  $(kel [%crib [%% %blur] [%% %blur] ~])

                -   [%crib wix=(list :[p=mark q=form])]
                  +:  %trop
                    [%cast [%crad 0] (form-lump kel)]
                    +-  %lome
                      +-  %cage
                        =>  axe   4
                        |-
                          ?-  wix.kel
                            -   [* ~]
                              +~
                                +:  %name
                                  p.i.wix.kel 
                                  [%mang $$(kel q.i.wix.kel) [%frag axe]]
                              ==

                            -   ^
                              +-
                                +:  %name
                                  p.i.wix.kel 
                                  +:  %mang
                                    $$(kel q.i.wix.kel) 
                                    [%frag (mul 2 axe)]
                                $(wix.kel t.wix.kel, axe (inc (mul 2 axe)))
                          ==

                -   [%pick bec=(list form)]
                  +:  %trop
                    [%cast [%crad 0] (form-lump ?-{bec.kel: - ^ i.bec.kel})]
                    +-  %lome
                      +:  %gril
                        [[%frag 4] ~]
                        |-
                          ?-  bec.kel
                            +   ~
                            -   ^   [[%fing i.bec.kel] $(bec.kel t.bec.kel)] 
                          == 
  
                -   [%rock hin=*]
                  +:  %trop
                    [%crad hin.kel]
                    +-  %lome
                      [%feng [%like [[%frag 4] ~] [%rock hin.kel]] [%frag 4]]

                -   [%gate puz=gene]
                  puz.kel

                -   [%grip ved=form piv=form]
                  $(kel piv.kel)

                -   [%dish heb=gene]
                  +:  %trop
                    [%cast [%crad 0] heb.kel]
                    [%lome [%feng [%like [[%frag 4] ~] heb.kel] [%frag 4]]]
              ==
        ==


    - mold
      |~
      ** 
        %atom
        %blot
        %blur
        [%cell p=mold q=mold]
        [%cone p=mold q=book]
        [%cube p=*]
        [%dome p=mold q=book]
        [%face p=mark q=mold]
        [%fork p=mold q=mold]
        [%fuse p=mold q=mold]
        [%hold p=mold q=gene]
      ==


    - gene
      |~
      **
        [%bail ~]                                       ::  !!
        [%cast fes=gene rum=gene]                       ::  ^-
        [%cage fut=(list gene)]                         ::  ++
        [%dbug lyq=gene]                                ::  !#
        [%home cux=home lyq=gene]                       ::  !:
        [%kick ved=wire suc=bolt]                       ::  :=
        [%like rid=wire bul=gene]                       ::  ?=
        [%link del=gene zim=gene]                       ::  ~>
        [%load pir=book]                                ::  |@
        [%lift pir=book]                                ::  |@
        [%name tem=mark caf=gene]                       ::  +=
        [%nock fiz={3 4 5 6} dil=gene]                  ::  :N
        [%quiz tes=gene bif=gene hob=gene]              ::  ?:
        [%rock gos=*]                                   ::  %=
        [%sure fes=gene gav=gene vep=gene]              ::  ^=
        [%spot xed=spot lyq=gene]                       ::  !@

        [%pont han=mark dur=gene leb=gene]              ::  =>
        [%rulf han=mark dur=gene leb=gene]              ::  =<
        [%trop hig=gene muc=gene]                       ::  ->
        [%prec hig=gene muc=gene]                       ::  -<
        [%colb del=gene zim=gene]                       ::  ~<

        [%feng tes=gene bif=gene]                       ::  ?>
        [%prox tes=gene hob=gene]                       ::  ?<
        [%gril mox=wire bem=(list pike)]                ::  ?-
        [%stam feg=gene bem=(list pike)]                ::  ?+
        [%boce das=(list gene)]                         ::  ?&
        [%dant das=(list gene)]                         ::  ?|
        [%blem gix=gene]                                ::  ?!

        [%malk vab=grid]                                ::  |+
        [%blin bov=gene vab=grid]                       ::  |*
        [%flic tep=form von=gene]                       ::  |:
        [%drol nix=form tep=form von=gene]              ::  ||
        [%lome cug=gene]                                ::  |=
        [%flot cug=gene]                                ::  |-

        [%lorb tul=form rum=gene]                       ::  ^:
        [%rald rum=gene]                                ::  ^*

        [%port tic=step vix=mark]                       ::  :<
        [%frag haz=axis]                                ::  :>
        [%mang buz=gene lep=gene]                       ::  :~
        [%garc buz=gene pum=(list gene)]                ::  :+
        [%flec buz=gene]                                ::  :-
        [%grun vec=bolt sud=gene]                       ::  :%
        [%nock fiz={3 4 5 6} dil=gene]                  ::  :N
      
        [%palt daq=@]                                   ::  %@
        [%crad gos=*]                                   ::  %*
      ==

    - pike
      |~
      **
        [%lask bic=form luf=gene]                       ::  -
        [%plic bic=form]                                ::  +
        [%semp bic=form luf=gene]                       ::  :
        [%fing bic=form]                                ::  =
        [%homp luf=gene]                                ::  *
      ==
        
    - form
      |~
      **
        %atom                                           ::  @
        %flag                                           ::  ?
        %blur                                           ::  *
        %twin                                           ::  ^
        [%crib wix=(list :[p=mark q=form])]             ::  *+
        [%dish heb=gene]                                ::  *~
        [%gate puz=gene]                                ::  *:
        [%grip ved=form piv=form]                       ::  *|
        [%pick bec=(list form)]                         ::  **
        [%rock hin=*]                                   ::  %
      ==

    - plug
      |~
      **
        @:mark
        [%pith tic=step vix=mark]
        [%port tic=step vix=mark]
        [%frag haz=axis]
      ==

    - book 
      (bush mark gene)

    - grid
      (list :[p=mark q=gene])

    - bolt
      (list :[p=wire q=gene])

    - wire
      (list plug)

    - home
      (list mark)

    - spot
      :[at=[lin=@ col=@] to=[lin=@ col=@]]

    - axis
      @

    - mark
      @

    - step
      @
  ==
