  ::
  ++  gain
    ~.  %gain
    |=  [gen=*gene]
    ^-  *type
    =-  ?:((orth(sut %noun) fiz) %void fiz)   ::  ?? - do we need this?
    ^=  fiz
    ?-    gen
        [%cslc *]   (cull & p:(seek %read ~(rake ap q.gen)) (play p.gen))
        [%csbr *]   |-(?~(p.gen sut $(p.gen t.p.gen, sut (lose i.p.gen))))
        [%cspm *]   |-(?~(p.gen sut $(p.gen t.p.gen, sut $$(gen i.p.gen))))
        [%zpcb *]   $(gen q.gen)
        [%zpdx *]   $(gen p.gen)
        *           sut
    ==
  ::
  ++  lose  
    ~.  %lose
    |=  [gen=*gene]  ^-  *type
    =-  ?:((orth(sut %noun) fiz) %void fiz)   ::  ?? - do we need this?
    ^=  fiz
    ?-    gen
        [%cslc *]   (cull | p:(seek %read ~(rake ap q.gen)) (play p.gen))
        [%csbr *]   |-(?~(p.gen sut $(p.gen t.p.gen, sut $$(gen i.p.gen))))
        [%cspm *]   |-(?~(p.gen sut $(p.gen t.p.gen, sut (gain i.p.gen))))
        [%zpcb *]   $(gen q.gen)
        [%zpdx *]   $(gen p.gen)
        *           sut
    ==
  ::
  ++  cull
    ~.  %cull
    |=  [pol=? axe=*axis ref=*type]
    ^-  *type
    ?:  =(1 axe)
      ?:(pol (fuse ref) (crop ref)))
    =+  [now=(cap axe) lat=(mas axe)]
    |-  ^-  *type
    ?-    sut
        [%atom *]   %void
        [%cell *]   
      ?:  =(2 now)
        (cell $$(axe lat, sut p.sut) q.sut)
      (cell p.sut $$(axe lat, sut q.sut))
    ::
        [%core *]   ?.(=(2 now) sut (core $$(axe lat, sut p.sut) q.sut))
        [%cube *]   (reco |=(p=*type $$(sut p)))
        [%face *]   (reco |=(p=*type (face p.sut $$(sut p))))
        [%fine *]   (reco |=(p=*type (fine p.sut q.sut $$(sut p))))
        [%fork *]   (fork $(sut p.sut) $(sut q.sut))
        [%hold *]   (reco |=(p=*type $$(sut p)))
        %noun       (reco |=(p=*type $$(sut p)))
        %void       %void
    ==
  ::
  ++  crop
    ~.  %crop
    |=  ref=*type
    =+  bix=*(set ~[*type *type])
    =<  dext
    &.
    ++  dext
      ^-  *type
      ?:  (~(has in bix) [sut ref])
        ~~  (dunk 'type')
        ~~  (dunk(sut ref) 'over')
        ~|(%crop-loop !!)
      =>  .(bix (~(put in bix) [sut ref]))
      ?:  |(?=(sut ref) =(%noun ref))
        %void
      ?:  =(%void ref)
        sut
      ?-    sut
          [%atom *] 
        ?-    ref
            [%atom *]   %void
            [%cell *]   sut
            *           sint
        ==
      ::
          [%cell *]   
        ?-  ref
          [%atom *]   sut
          [%cell *]   (cell $(sut p.sut, ref p.ref) $(sut q.sut, ref q.ref))
          *           sint
        ==
      ::
          [%core *]   sut
          [%cube *]
        ?:  &(?=([%cube *] ref) =(p.sut p.ref))
          %void
        ?-    ref
            *{[%atom *] [%cell *]}
          =+  foz=$(sut q.sut)
          ?:  (firm(sut foz) p.sut)
            (cube p.sut foz)
          %void
        ::
            *  sint
        ==
      ::
          [%face *]   (face p.sut $(sut q.sut))
          [%fine *]   (fine p.sut q.sut $(sut r.sut))
          [%fork *]   (fork $(sut p.sut) $(sut q.sut))
          [%hold *]   (reco |=(a=*type $$(sut a)))
          %noun       (reco |=(a=*type $$(sut a)))
          %void       %void
      ==
    ::
    ++  sint
      ^-  *type
      ?-    ref
        *{%atom %cell}  !!
        [%fork *]       dext(sut dext(ref p.ref), ref q.ref)
        %void           sut
        *               dext(ref repo(sut ref))
      ==
    --
  ::
  ++  mill
    ~.  %mill
    |=  [gol=*type old=*(unit type) gen=*gene]
    ^-  [p=[p=*type q=*(unit type)] q=*form]
    =<  $
    &.
    ++  boil
      |=  sel=|%(*form)
      ^-  *form
      ?.(fab [0 0] $.sel)
    ::
    ++  coke
      |=  [nug=*form]
      ?-    nug
          [0 *]   p.nug
          [10 *]  $(nug q.nug)
          *       ~|(%mint-coke !!)
      ==
    ::
    ++  crow
      |=  [mel=*{%gold %iron %lead %zinc} ruf=*gene dab=*(map term foot)]
      ^-  [[p=*type q=*(unit type)] q=*form]
      =+  dan=$$(gen ruf, gol %noun)
      ?.  fab
        [[(nice (core p.p.dan [mel p.p.dan [[0 0] dab] [0 0]])) ~] [0 0]]
      =+  toc=(core p.p.dan [%gold p.p.dan [~ dab]])
      =+  dez=(bake(sut toc) dab)
      :-  :-  (nice (core p.dan mel p.dan [dez dab]))
          (bind q.p.dan |=(a=*type (core a [mel a [~ dab]]))
      (cons q.dan [1 dez])
    ::
    ++  nice
      |=  typ=*type
      ?.  ?|(!vet (nest(sut gol) typ))
        ~~  (dunk(sut gol) 'need')
        ~~  (dunk(sut typ) 'have')
        ~|('mint-nice' !!)
      typ
    ::
    ++  whip
      |=  mog=|+([a=? b=*type] [p=*type q=*form])
      ^-  [p=[p=*type q=*(unit type)] q=*form]
      =+  voy=(mog vet sut)
      ?~  old
        [[p.voy ~] q.voy]
      =+  yog=(mog | u.old)
      ?.  =(q.yog q.voy)
        ~|(%mill-mate !!)
      [[p.voy [~ q.yog]] q.voy]
    ::
    ++  whop
      |=  mog=|%([p=*type q=*form])
      ^-  [p=[p=*type q=*(unit type)] q=*form]
      =+  voy=$:mog
      ?~  old
        [[p.voy ~] q.voy]
      [[p.voy [~ p.voy]] q.voy]
    ::
    ++  $
      ^-  [p=[p=*type q=*(unit type)] q=*form]
      ?-    gen
          [%cslc *]                                                   ::  ?=
        %-  whop
        :-  [(nice flag) ~]
        (boil |.((fish(sut (play p.gen)) (coke q:$$(gen q.gen, gol %noun)))))
      ::
          [%csdg *]                                                   ::  ?:
        =+  nor=$(gen p.gen, gol flag)
        =+  fex=(gain p.gen)
        =+  fay=(lose p.gen)
        ?~  old
          ?:  =(%void fex)
            ?:  =(%void fay)
              [[%void ~] [0 0]]
            $(sut fay, gen r.gen)
          ?:  =(%void fay)
            $(sut fex, gen q.gen)
          =+  hiq=$(sut fex, gen q.gen)
          =+  ran=$(sut fay, gen r.gen)
          [[(fork p.hiq p.ran) ~] (boil |.([6 q.nor q.hiq q.ran]))]
        =+  yer=(gain(sut u.old) p.gen)
        =+  rus=(lose(sut u.old) p.gen)
        ?:  |(&(=(%void yer) !=(%void fex)) &(=(%void rus !=(%void fay))))
          ~|(%mill-mate !!)
        ?:  =(%void fex)
          ?:  =(%void fay)
            [[%void ~] [0 0]]
          $(sut fay, old [~ rus], gen r.gen)
        ?:  =(%void fay)
          $(sut fex, old [~ yer], gen q.gen)
        =+  hiq=$(sut fex, old [~ yer], gen q.gen)
        =+  ran=$(sut fay, old [~ rus], gen r.gen)
        :-  :-  (fork p.p.hiq p.p.ran)
            [~ (fork (need q.p.hiq) (need q.p.ran))]
        (boil |.([6 q.nor q.hiq q.ran]))
      ::
          [%dgsp *]                                                   ::  :-
        =+  hed=$(gen p.gen, gol %noun)
        =+  tal=$(gen q.gen, gol %noun)
        :-  :-  (nice (cell p.p.hed p.p.tal))
            ?~  old
              ~
            (cell (need q.p.hed) (need q.p.tal))
        (boil |.((cons q.hed q.tal)))
      ::
          [%dtlc *]                                                   ::  .=
        %-  whop
        :-  (nice flag)
        (boil |.([5 q:$$(gen p.gen, gol %noun) q:$$(gen q.gen, gol %noun)]))
      ::
          [%dtcs *]                                                   ::  .?
        %-  whop
        :-  (nice flag)
        (boil |.([3 q:$$(gen p.gen, gol %noun)]))
      ::
          [%dtps *]                                                   ::  .+
        %-  whop
        :-  (nice [%atom %%])
        (boil |.([4 q:$$(gen p.gen, gol [%atom %%])]))
      ::
          [%dtsg *]                                                   ::  .~ 
        %-  whop
        ?:  .?(q.gen)
          [(nice [%cube q.gen %noun]) [1 q.gen]]
        [(nice [%cube q.gen %atom p.gen]) [1 q.gen]]
      ::
          [%dttr *]                                                   ::  .*
        %-  whop
        :-  (nice %noun)
        (boil |.([2 q:$$(gen p.gen, gol %noun) q:$$(gen q.gen, gol %noun)]))
      ::
          [%dtwt *]                                                   ::  .@
        %-  whop
        [(nice [%atom p.gen]) [1 q.gen]]
      ::
          [%ktlc *]                                                   ::  ^=
        =+  vat=$(gen q.gen)
        :-  :-  (face p.gen p.p.vat)
            (bind q.p.vat |=(a=*type (face p.gen a)))
        q.vat
      ::
          [%ktdl *]                                                   ::  ^<
        =-  :-  :-  (nice (fine %very (play p.gen) p.p.ryd)) 
                q.p.ryd
            q.ryd
        ^=  ryd  =-  $(gen hos, gol %noun)
        ^=  hos
        :+  %lcps  [%ktlc %a p.gen]                   ::  =+  a=<p.gen>
        :+  %lcps  [%ktlc %b [%lcld [~ 2] q.gen]]     ::  =+  b==>(.2 <q.gen>)
        :+  %csld  [%mtsp %a %b ~]                    ::  ?>  (a b)
        %b                                            ::  b
      ::
          [%ktld *]                                                   ::  ^>
        =-  :-  :-  (nice (fine %pure (play p.gen) p.ryd))
                q.p.ryd
            q.ryd
        ^=  ryd  =-  $(gen hos, gol %noun)
        ^=  hos
        :+  %lcps  [%ktlc %a p.gen]                   ::  =+  a=<p.gen>
        :+  %lcps  [%ktlc %b [%lcld [~ 2] q.gen]]     ::  =+  b==>(.2 <q.gen>)
        :-  %brsp  :+  %ktsp  [%mttr %a]              ::  |-  ^-  *a
        :+  %lcps  [%ktlc %c [%mtsp %a %b ~]]         ::  =+  c=(a b)
        :^  %csdg  [%dtlc %b %c]                      ::  ?:  =(b c)
          %c                                          ::    c
        [%mtlc [%% ~] [[%b %c] ~]]                    ::  $(b c)
      ::
          [%ktsg *]                                                   ::  ^~
        =+  nef=$(gen p.gen)
        ~~  (dunk %ktsg)
        [p.nef (boil |.([1 ~|(%mint-burn .*(burn q.nef))]))]
      ::
          [%ktsp *]                                                   ::  ^-
        =+  hif=(nice (play p.gen))
        :-  :-  hif
            (bind old |=(a=*type (play(
        [hif (boil |.(q:$$(gen q.gen, gol hif)))]
      ::
          [%lcld *]                                                   ::  =>
        =+  lem=$(gen p.gen, gol %noun)
        =+  soq=$(sut p.p.lem, old q.p.lem, gen q.gen)
        [p.soq (boil |.((comb q.lem q.soq)))]
      ::
          [%mtlc *]                                                   ::  %=
        %+  whip  vet  |.
        =+  lar=(seek %read p.gen)
        ?-    -.q.lar
            & 
          =+  fup=(emit p.lar [[p.q.lar [& ~ 1]] ~] q.gen)
          [(nice -<.p.fup) q.fup]
        ::
            |
          =+  fup=(emit p.lar q.q.lar q.gen)
          [(nice (fire p.fup)) [9 p.q.lar q.fup]]
        ==
      ::
          [%pmdg *]                                                   ::  &:
        (crow %zinc [%dgsp [~ 1] p.gen] q.gen)
      ::
          [%pmdt *]                                                   ::  &.
        (crow %gold [~ 1] p.gen)
      ::
          [%pmmt *]                                                   ::  &%
        (crow %lead [~ 1] p.gen)
      ::
          [%pmps *]                                                   ::  &+
        (crow %iron [%dgsp [~ 1] p.gen] q.gen)
      ::
          [%sgld *]                                                   ::  ~>
        =+  hum=$(gen q.gen)
        :-  p.hum
        %-  boil  |.
        :+  10
          ?-    p.gen
              @   p.gen
              ^   [p.p.gen q:$$(gen q.p.gen, gol %noun)]
          ==
        q.hum
      ::
          [%zpcm *]                                                   ::  !;
        :-  (nice (play p.gen))                    ::  XX validate!
        [1 q.gen]
      ::
          [%zpcb *]                                                   ::  !_
        ~~  (show %o p.gen)
        =+  hum=$(gen q.gen)
        [p.hum [10 [%spot 1 p.gen] q.hum]]
      ::
          [%zpdx *]                                                   ::  !#
        ~#
        $(gen p.gen)
      ::
          [%zplc *]                                                   ::  !=
        :-  (nice %noun)
        (boil |.([1 q:$$(vet |, gen p.gen)]))
      ::
          [%zpmt ~]                                                   ::  !%
        =+  pet=seed
        [(nice p.pet) [1 q.pet]]
      ::
          [%zptm *]                                                   ::  !;
        =+  vos=$(gol %noun, gen q.gen)
        :-  (nice (cell (play p.gen) p.vos))      ::  XX validate!
        (boil |.((cons [1 p.vos] q.vos)))
      ::
          [%zpzp ~]                                                   ::  !!
        :-  %void
        [0 0]
      ::
          *           
        =+  doz=~(open ap gen) 
        ?:  =(doz gen)
          ~~  (show [%c 'gene'] [%q gen])
          ~|(%mint-open !!)
        $(gen doz)
      ==
    --

