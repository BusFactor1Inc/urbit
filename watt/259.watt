?>  ?=(@ .)
%.  .
=<  dec
=>  259
=>  
  ::::
  ::::  Tier 0, kernel stub
  ::::
  ~%    %k.259
      ~
    ~
  &.
  ++  stub  259
  ++  nat   |=(a=* ?>(?=(@ a) a))
  ++  all   |=(a=* a)
  ++  quo   |=(a=* ?-(a & &, | |))
  --
=>
  ::::
  ::::  Tier 1, atomic arithmetic
  ::::
  ~%    %a
      -
    ~
  &.
  ++  add
    ~.  %add
    |=  [a=*nat b=*nat] 
    ^-  @
    ?:  .=(0 a)
      b
    $(a (dec a), b .+(b))
  ::
  ++  dec
    ~.  %dec
    |=  a=*nat
    ^-  *nat
    ~|  %dec
    ?<  .=(0 a)
    =+  b=`*nat`0
    |-
    ?:  .=(a .+(b))
      b
    $(b .+(b))
  ::
  ++  div
    ~.  %div
    |=  [a=*nat b=*nat]
    ^-  *nat
    ~|  %div
    ?<  .=(0 b)
    =+  c=`*nat`0
    |-
    ?:  (lth a b)
      c
    $(a (sub a b), c .+(c))
  ::
  ++  gte
    ~.  %gte
    |=  [a=*nat b=*nat]
    ^-  ?
    !(lth a b)
  ::
  ++  gth
    ~.  %gth
    |=  [a=*nat b=*nat]
    ^-  ?
    !(lte a b)
  ::
  ++  lte
    ~.  %lte
    |=  [a=*nat b=*nat]
    ?|(.=(a b) (lth a b))
  ::
  ++  lth
    ~.  %lth
    |=  [a=*nat b=*nat]
    ^-  ?
    ?&
      !.=(a b)
      |-  ?:  .=(0 a)
            &
          ?:  .=(0 b)
            |
          $(a (dec a), b (dec b))
    ==
  ::
  ++  max
    ~.  %max
    |=  [a=*nat b=*nat]
    ^-  *nat
    ?:  (gth a b)
      a
    b
  ::
  ++  min
    ~.  %min
    |=  [a=*nat b=*nat]
    ^-  *nat
    ?:  (lth a b)
      a
    b
  ::
  ++  mod
    ~.  %mod
    |=  [a=*nat b=*nat]
    ^-  *nat
    ?<  .=(0 b)
    (sub a (mul b (div a b)))
  ::
  ++  mul
    ~.  %mul
    |=  [a=*nat b=*nat]
    ^-  *nat
    =+  c=`*nat`0
    |-
    ?:  .=(0 a)
      c
    $(a (dec a), c (add b c))
  ::
  ++  sub
    ~.  %sub
    |=  [a=@ b=@]
    ^-  @
    ?:  .=(0 b)
      a
    $(a (dec a), b (dec b))
  ::
  ::  Tier 2 cones
  ::
  ++  list
    |*  a=all
    |=  b=*
    ?-    b
        ~           ~
        [i=* t=*]   [i=(a i.b) t=$(b t.b)]
    ==
  ::
  ++  tree
    |*  a=<*>
    |=  b=*
    ?-    b
        ~               ~
        [n=* l=* r=*]   [n=(a n.b) l=$(b l.b) r=$(b r.b)]
    ==
  ::
  ++  unit
    |*  a=<*>
    |=  b=*
    ?-    b
        ~       ~
        [~ *]   [~ u=(a +.b)]
    ==
  --
.
