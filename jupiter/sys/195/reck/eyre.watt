!:
::          %eyre, security manager.   This file is in the public domain.
::
:-  %eyre
^-  dock
|=  [now=time wha=@ta]
%^    hull
    (iris now)
  wha
^-  bead
|=  [now=time wha=@ta]
:-  ^-  lime
    :-  %all
    :~  :^    %rod
            %abel
          :-  [~ ~]
          :-  [%unix %term ~]
          |=  hap=*  ^-  (unit)
          ?>  ?=([p=@ta q=@ta ~] hap)
          :-  ~
          :~  %unix
              %term
              ~(rent co ~ %p (need (clan %p p.hap)))
              ~(rent co ~ %ud (need (clan %ud q.hap)))
          ==
        |=  [seq=@ud muz=* cax=bill] 
        [%run %inpb muz q.cax]
    ::
        :^    %rod
            %boaz
          :-  [~ ~]
          :-  [%unix %pack ~]
          |=  hap=*  ^-  (unit)
          [~ ~]
        |=  [seq=@ud muz=* cax=bill] 
        [%run %pack q.cax]
    ::
        :^    %rod
            %cain
          :-  [~ ~]
          :-  [%bach %pout ~]
          |=  hap=*  ^-  (unit)
          ?>  ?=([p=@ta ~] hap)
          [~ (need (clan %p p.hap))]
        |=  [seq=@ud muz=* cax=bill]
        [%run %pout muz q.cax]
    ==
^-  vane
=>  ..$
=>  |%
    ++  game
      $:  gem=(map plot chum)                           ::  hashed passcodes
          liv=(map plot (list path))                    ::  live sessions
          rev=(map path plot)                           ::  identities
      == 
    ++  move                                            ::  application event
      $%  [%pack p=rock]                                ::  packet input
          [%inpb p=path q=card-b]                       ::  card input
          [%pout p=plot q=(unit path) r=gift]           ::  gift output
      ==
    --
=+  sys=*game
|%
++  peek  |=([cam=lens hap=path] ~)
++  poke  
  |=  [cam=lens man=*] 
  ^-  [p=lime q=vane]
  =+  muv=((hard move) man)
  ?-    -.muv
      %pack
    :-  [%say %pi [%arvo ~] [%pack p.muv]]
    +>.$
  ::
      %inpb
    =+  ^=  yex  ^-  [p=plot q=game]
        =+  vep=(~(get by rev.sys) q.muv)
        ?^  vep  [u.vep sys]
        =+  bog=(shaf %bogus when:cam)
        [bog sys(rev (~(put by rev.sys) p.muv bog))]
    :-  [%say %pi [%bach ~(rent co ~ %p p.yex) ~] [p.muv q.muv]]
    +>.$(sys q.yex)
  ::
      %pout
    :-  ?+  -.r.muv
          [%say %yo %g r.muv]
          %hear  [%say %pi [%arvo ~] p.muv]
          %task  [%say %pi [%task p.r.muv ~] q.r.muv]
        ==
    +>.$
  ==
--
