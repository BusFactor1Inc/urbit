!:
::          %eyre, security manager.   This file is in the public domain.
::
:-  %eyre
^-  dock
|=  [now=time wha=@ta]
%^    hull
    (iris now)
  wha
^-  bead
|=  [now=time wha=@ta]
:-  ^-  lime
    :-  %all
    :~  :^    %rod
            %abel
          :-  [~ ~]
          :-  [%unix %term ~]
          |=  hap=*  ^-  (unit)
          ?>  ?=([p=@ta q=@ta ~] hap)
          :-  ~
          :~  %unix
              %term
              ~(rent co ~ %p (need (clan %p p.hap)))
              ~(rent co ~ %ud (need (clan %ud q.hap)))
          ==
        |=  [seq=@ud muz=* cax=bill] 
        [%run %i %b muz q.cax]
    ::
        :^    %rod
            %boaz
          :-  [~ ~]
          :-  [%unix %pack ~]
          |=  hap=*  ^-  (unit)
          [~ ~]
        |=  [seq=@ud muz=* cax=bill] 
        [%run %i %a q.cax]
    ::
        :^    %rod
            %cain
          :-  [~ ~]
          :-  [%bach %pout ~]
          |=  hap=*  ^-  (unit)
          ?>  ?=([p=@ta ~] hap)
          [~ (need (clan %p p.hap))]
        |=  [seq=@ud muz=* cax=bill]
        [%run %o %b muz q.cax]
    ==
^-  vane
=>  ..$
=>  |%
    ++  game
      $:  gem=(map plot chum)                           ::  hashed passcodes
          liv=(map plot (list path))                    ::  live sessions
          rev=(map path plot)                           ::  identities
      == 
    ++  move                                            ::  application event
      $%  :-  %i
            ^=  p
            $%  [%a p=card-a]
                [%b p=path q=card-b]
            ==
          :-  %o
            ^=  p
            $%  [%a p=gift-a]
                [%b p=@p q=(unit path) r=gift-b]
            ==
      ==
    --
=+  sys=*game
|%
++  peek  |=([cam=lens hap=path] ~)
++  poke  
  |=  [cam=lens man=*] 
  ^-  [p=lime q=vane]
  =+  muv=((hard move) man)
  ?-    -.muv
      %i
    ?-    -.p.muv
        %a
      !!
    ::
        %b
      =+  ^=  yex  ^-  [p=plot q=game]
          =+  vep=(~(get by rev.sys) q.p.muv)
          ?^  vep  [u.vep sys]
          =+  bog=(shaf %bogus when:cam)
          [bog sys(rev (~(put by rev.sys) p.p.muv bog))]
      :-  [%say %pi [%bach ~(rent co ~ %p p.yex) ~] [p.p.muv q.p.muv]]
      +>.$(sys q.yex)
    ==
  ::
      %o
    ?-    -.p.muv
        %a
      !!
    ::
        %b
      :-  ?+  -.r.p.muv
            [%say %yo %g r.p.muv]
            %hear  [%say %pi [%arvo ~] p.p.muv]
            %task  [%say %pi [%task p.r.p.muv ~] q.r.p.muv]
          ==
      +>.$
    ==
  ==
--
