  ::
  ++  fern
    ~.  %fern
    |=  [dox=*type gen=*gene]
    ^-  ?
    ?:  (~(has in rib) [sut dox gen])
      &
    =>  .(rib (~(put in rib) [sut dox gen]))
    =+  gol=*type
    =<  =+  hel=$
        &
    &.
    ++  crow
      |=  [mel=*<%gold %lead %iron> ruf=*gene dab=*(map term foot)]
      =+  dan=$$(gen ruf, gol %noun)
      =+  ^=  toc 
          :-  (core p.dan [%gold p.dan [~ dab]])
          (core q.dan [%gold q.dan [~ dab]])
      =+  ^=  hel
          |-  ^-  ?
          ?~  dab
            &
          ?>  ?=([& *] q.n.dab)
          =+  hel=$$$(vet ?:(p.p.q.n.dab vet |), gol %noun, gen q.p.q.n.dab)
          ?-    dab
              [* ~ ~]   &
              [* ~ *]   $(dab r.dab)
              [* * ~]   $(dab l.dab)
              [* * *]   ?&($(dab l.dab) $(dab r.dab))
          --
      :-  (nice (core p.dan mel p.dan [~ dab]))
      (core q.dan mel q.dan [~ dab])
    ::
    ++  nice
      |=  typ=*type
      ?.  ?|(!vet (nest(sut gol) typ))
        ~~  dump(sut gol)
        ~~  dump(sut typ)
        ~|('fern-nice' !!)
      typ
    ::
    ++  $
      ^-  [p=*type q=*type]
      ?-    gen
          [%bnld *]
        =+  fid=$(gen p.gen, gol %noun)  
        $(sut p.fid, dox q.fid, gen q.gen)
      ::
          [%csbn *]
        =+  hel=$(gen q.gen, gol %noun)
        [(nice flag) flag]
      ::
          [%csdg *]
        =+  nor=$(gen p.gen, gol flag)
        =+  fex=[p=(gain p.gen) q=(gain(sut dox) p.gen)]
        ?:  .=(%void p.fex)
          $(gen r.gen)
        ?:  .=(%void q.fex)
          ~|(%fern-lump !!)
        =+  hiq=$(sut p.fex, dox q.fex, gen q.gen)
        =+  ran=$(gen r.gen)
        [(fork p.hiq p.ran) (fork q.hiq q.ran)]
      ::
          [%dgdp *]
        =+  hed=$(gen p.gen, gol %noun)
        =+  tal=$(gen q.gen, gol %noun)
        [(nice (cell p.hed p.tal)) (cell q.hed q.tal)]
      ::
          [%dtbn *]
        =+  hel=[$(gen p.gen, gol %noun) $(gen q.gen, gol %noun)]
        [(nice flag) flag]
      ::
          [%dtcs *]
        =+  hel=$(gen p.gen, gol %noun)
        [(nice flag) flag]
      ::
          [%dtpd *]
        =+  hel=$(gen p.gen, gol %atom)
        [(nice %atom) %atom]
      ::
          [%dtsg *]
        =+  nik=[%cube p.gen]
        [(nice nik) nik]
      ::
          [%dttr *]
        =+  hel=[$(gen p.gen, gol %noun) $(gen q.gen, gol %noun)]
        [(nice %noun) %noun]
      ::
          [%ktbn *]
        =+  vat=$(gen q.gen)
        [(face p.gen p.vat) (face p.gen q.vat)]
      ::
          [%ktdp *]
        =+  hif=[p=(nice (play p.gen)) q=(play(sut dox) p.gen)]
        =+  hel=$(gen q.gen, gol p.hif)
        hif
      ::
          [%ktpd *]
        =+  zut=(play p.gen)
        ?.  ?|(!vet (nest(sut gol) zut))
          ~|(%fern-cool !!)
        $(gen q.gen, gol zut)
      ::
          [%ktsg *]  $(gen p.gen)
          [%mtbn *]
        =+  dow=(mint gol gen)
        =+  rux=(mint(sut dox, vet |) gol gen)
        ?.  .=(q.dow q.rux)
          ~|(%fern-rage !!)
        [p.dow p.rux]
      ::
          [%pmdg *]  (crow %lead [%dgdp p.gen [~ 1]] q.gen)
          [%pmdt *]  (crow %gold [~ 1] p.gen)
          [%pmpd *]  (crow %iron [%dgdp p.gen [~ 1]] q.gen)
      ::
          [%sgld *]  $(gen q.gen)
          [%zpbn *]  [%noun %noun]
          [%zpcb *]  $(gen q.gen)
          [%zpdx *]  $(gen p.gen)
          [%zpzp ~]  [%void %void]
          *           
        =+(doz=~(open ap gen) ?:(.=(doz gen) ~|(%play-open !!) $(gen doz)))
      --
    ==

